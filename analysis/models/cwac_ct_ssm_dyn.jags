# Model considers one time series evolving in continuous time

model {

  # PRIORS -----------------------------------------------------------------

  tau.B ~ dunif(log(1), 1/log(10))
  tau.zeta ~ dunif(log(1), 1/log(3))
  tau.eps ~ dunif(log(1), 1/log(3))
  tau.alpha ~ dunif(log(1), 1/log(10))
  tau.e ~ dunif(log(1), 1/log(10))
  tau.o ~ dunif(log(1), 1/log(10))

  sig.B = 1/tau.B
  sig.zeta = 1/tau.zeta
  sig.eps = 1/tau.eps
  sig.alpha = 1/tau.alpha
  sig.e = 1/tau.e
  sig.o = 1/tau.o


  # MODEL FOR POPULATION INCREMENTS ----------------------------------------

  # model for summer population increments
  beta[1] ~ dnorm(log(1), log(2)) # prior for initial long-term growth rate

  # model for winter population increments
  lambda[1] ~ dnorm(0, 0.001)

  # MODEL FOR POPULATION LATENT STATE ---------------------------------------

  mu[1] ~ dunif(log(1), log(1500)) # prior for initial population size

  for(t in 1:(N-1)){

    B[t] ~ dnorm(0, tau.B/sqrt(dt[t]))
    mu[t+1] = mu[t] + beta[t]*summer[t] + lambda[t]*winter[t] + B[t]

    zeta[t] ~ dnorm(0, tau.zeta/sqrt(dt[t]))
    eps[t] ~ dnorm(0, tau.eps/sqrt(dt[t]))

    beta[t+1] = beta[t] + zeta[t]
    lambda[t+1] = lambda[t] + eps[t]

  }


  # MODEL FOR OBSERVATION PROCESS -----------------------------------------

  for(t in 1:N){
    obs[t] ~ dnorm(mu[t], tau.alpha * summer[t] +
                          tau.e * winter[t] +
                          tau.o - tau.o * (summer[t] + winter[t]))
  }

}
